plugins {
	id 'maven-publish'
}

apply plugin: 'java'

version = '3.0.0'
group = 'de.siphalor'
description = """Hjson, the Human JSON."""

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_16

sourceSets {
	main {
		java {
			srcDir 'src/main'
		}
	}
	test {
		java {
			srcDir 'src/test'
		}
		resources {
			srcDir 'assets'
		}
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set("javadoc")
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

jar {
	manifest {
		attributes(
				'Hjson-Version': project.version,
		)
	}
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

task(testSuite, dependsOn: ['classes','testClasses'], type: JavaExec) {
	main('org.hjson.test.Main')
	classpath = files(sourceSets.main.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

test.dependsOn testSuite

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = "$archivesBaseName-java16"
			artifact(jar)
			artifact(sourcesJar)
		}
	}

	repositories {
		if (project.hasProperty("siphalorMavenUser")) {
			maven {
				url = "https://maven.siphalor.de/upload.php/"
				name = "Siphalor"
				credentials {
					username = siphalorMavenUser
					password = siphalorMavenPassword
				}
			}
		}
	}
}
